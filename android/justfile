build profile='dev':
    #!/usr/bin/env bash
    set -euo pipefail

    case "{{profile}}" in
        dev)
            ./gradlew assembleBluetoothDebug
            ;;
        release)
            ./gradlew assembleBluetoothRelease
            ;;
        *)
            echo Invalid profile
            exit 1
    esac

test:
    cargo test
    ./gradlew testDebugUnitTest

test-cov:
    cargo llvm-cov --no-report

ktlint-paths := "build.gradle.kts app/build.gradle.kts app/src/**/*.kt"
alias fmt := format
format:
    -cargo fmt
    -ktlint --editorconfig=../.editorconfig -F {{ ktlint-paths }}

format-check:
    #!/usr/bin/env bash
    set -euo pipefail

    cargo fmt --check
    ktlint --editorconfig=../.editorconfig {{ ktlint-paths }}
